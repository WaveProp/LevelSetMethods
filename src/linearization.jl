abstract type AbstractDual{N,T} <: Number end

"""
    GradientDual{N,T}

Structure used to reprensent the gradient of a function `f : â„á´º â†’ T` for the
purpose of performing forward-mode automatic differentiation.
"""
struct GradientDual{N,T} <: AbstractDual{N,T}
    Î±::T
    Î²::SVector{N,T}
end

val(l::GradientDual)    = l.Î±
gradient(l::GradientDual) = l.Î²

"""
    struct LinearizationDual{D,T<:Real}

Linearization of a `D`-dimensional function `f : ð‘á´° â†’ T` with a strict bound on
the remainder.

`LinearizationDual` objects are constructed given a function/functor `f` and a
`HyperRectangle` `rec` using `l = LinearizationDual(f,rec)`. The object `l`
represents an approximation of `f` inside of `rec` in the following sense:
`|f(ð±) - Î± - Î²â‹…(ð± - ð±â‚€)| < Ïµ âˆ€ ð± âˆˆ rec`, where `l = LinearizationDual(f,rec)` and `Î± =
value(l)`, `Î² = gradient(l)`, `Ïµ = remainder(l)`.
"""
struct LinearizationDual{D,T} <: AbstractDual{D,T}
    Î±::T
    Î²::SVector{D,T}
    Ïµ::T
    rec::HyperRectangle{D,T}
end

"""
    value(l::Linearization)

Value of `l` at the center of the `domain(l)`.
"""
val(l::LinearizationDual) = l.Î±

"""
    gradient(l::Linearization)

Gradient of `l` at the center of the `domain(l)`.
"""
gradient(l::LinearizationDual) = l.Î²

"""
    remainder(l::Linearization)

An upper bound for the remainder of `l`.
"""
remainder(l::LinearizationDual) = l.Ïµ

"""
    domain(l::Linearization) --> HyperRectangle

Domain of validity of the linearization `l`.
"""
domain(l::LinearizationDual) = l.rec

## addition
Base.:+(u::GradientDual, c::Real) = GradientDual(val(u) + c, gradient(u))
Base.:+(c::Real, u::GradientDual) = u + c
Base.:+(u::LinearizationDual, c::Real) = LinearizationDual(val(u) + c, gradient(u), remainder(u), domain(u))
Base.:+(c::Real, u::LinearizationDual) = u + c
function Base.:+(u::LinearizationDual{N,T}, v::LinearizationDual{N,T}) where {N,T}
    rec = _result_domain(u,v)
    LinearizationDual(val(u) + val(v), gradient(u) + gradient(v), u.Ïµ + v.Ïµ, rec)
end
function Base.:+(u::GradientDual, v::GradientDual)
    GradientDual(val(u) + val(v), gradient(u) + gradient(v))
end

## subtraction
Base.:-(u::LinearizationDual) = (-1) * u
Base.:-(u::GradientDual) = (-1) * u
Base.:-(u::LinearizationDual, c::Real) = u + (-c)
Base.:-(u::GradientDual, c::Real) = u + (-c)
Base.:-(c::Real, u::LinearizationDual) = c + (-u)
Base.:-(c::Real, u::GradientDual) = c + (-u)
Base.:-(u::LinearizationDual, v::LinearizationDual) = u + (-v)
Base.:-(u::GradientDual, v::GradientDual) = u + (-v)

## multiplication
Base.:*(u::LinearizationDual, c::Real) = LinearizationDual(val(u) * c, gradient(u) * c, abs(c) * remainder(u), domain(u))
Base.:*(u::GradientDual, c::Real) = GradientDual(val(u) * c, gradient(u) * c)
Base.:*(c::Real, u::LinearizationDual) = u * c
Base.:*(c::Real, u::GradientDual) = u * c
function Base.:*(u::LinearizationDual, v::LinearizationDual)
    rec = _result_domain(u,v)
    Î´ = half_width(u)
    l1 = dot(abs.(gradient(u)), Î´)
    l2 = dot(abs.(gradient(v)), Î´)
    LinearizationDual(val(u) * val(v), val(u) * gradient(v) + val(v) * gradient(u), l1 * l2 + (abs(val(u)) + l1) * remainder(v) + (abs(val(v)) + l2) * remainder(u) + remainder(u) * remainder(v), rec)
end

# since `LinearizationDual` behaves like a number, multiplying a `LinearizationDual` by
# a vector of the same type is handled like scalar*vector
function Base.:*(Î±::LinearizationDual{N, T}, Î²::SVector{M, LinearizationDual{N, T}}) where {N,M,T}
    map(b -> Î±*b,Î²)
end

function Base.:*(u::GradientDual, v::GradientDual)
    GradientDual(val(u) * val(v), val(u) * gradient(v) + val(v) * gradient(u))
end

# division
Base.:/(u::LinearizationDual, c::Real) = LinearizationDual(val(u) / c, gradient(u) / c, remainder(u) / abs(c), domain(u))
Base.:/(u::GradientDual, c::Real) = GradientDual(val(u) / c, gradient(u) / c)

# dual basis
function linearization_basis(rec::HyperRectangle{D,T}) where {D,T}
    xc = center(rec)
    xÌ‚ = svector(D) do dim
        Î² = svector(i -> i == dim ? one(T) : zero(T), D)
        LinearizationDual(xc[dim], Î², zero(T), rec)
    end
    return xÌ‚
end

function gradient_basis(x::SVector{D,T}) where {D,T}
    xÌ‚ = svector(D) do dim
        Î² = svector(i -> i == dim ? one(T) : zero(T), D)
        GradientDual(x[dim], Î²)
    end
    return xÌ‚
end

# TODO: document
function linearization(f, rec::HyperRectangle)
    xÌ‚ = linearization_basis(rec)
    f(xÌ‚)
end

# TODO: document
function gradient(f, x::SVector)
    xÌ‚ = gradient_basis(x)
    gradient(f(xÌ‚))
end

# power
function Base.:^(l::LinearizationDual, p::Integer)
    if p == 1
        return l
    else
        l * (l^(p - 1))
    end
end

function Base.:^(l::GradientDual, p::Integer)
    @assert p â‰¥ 1
    if p == 1
        return l
    else
        l * (l^(p - 1))
    end
end

# TODO: add cos/sin?

"""
    bound(f,rec::HyperRectangle)

Return bounds `(lb,ub)` such that `lb â‰¤ f(x) â‰¤ ub` for `x âˆˆ rec`. By default,
bounds are computed using a bounded [`LinearizationDual`](@ref) of `f` on `rec`.

This method can be overloaded for specific types `typeof(f)` if a more efficient
way of computing the bound is known (e.g. if `f` is affine).
"""
function bound(f, rec::HyperRectangle{D}) where {D}
    fÌ‚ = linearization(f, rec)
    bound(fÌ‚)
end

# HACK: to have the partial derivatives as SVector of functions we simply wrap
# an ntuple around the gradient function. Not the most efficient way, but does
# not seem to affect the performance in any significant way. A better way could
# be to get the partial independently through autodiff, but this would require
# further changes to the code used in gradient.
function partials(Ï•,::Val{D}) where {D}
    f = (x) -> gradient(Ï•,x)
    svector(i-> x -> f(x)[i],D)
end

half_width(l::LinearizationDual) = half_width(domain(l))

bound(x::Number,rec=nothing) = (x, x)

function bound(l::LinearizationDual)
    Î± = val(l)
    Î´ = half_width(l)
    Î² = gradient(l)
    Î” = dot(abs.(Î²), Î´) + remainder(l)
    Î± - Î”, Î± + Î”
end

bound(l::SVector{<:Any,<:LinearizationDual}) = bound.(l) # for gradients

# required by `gradient_basis` to autodiff through `LinearizationDual`
function Base.one(::Type{LinearizationDual{N, T}}) where {N,T}
    Î± = one(T)
    Î² = svector(i->zero(T),N)
    Ïµ = zero(T)
    rec = HyperRectangle(Î²,Î²)
    LinearizationDual(Î±,Î²,Ïµ,rec)
end
function Base.zero(::Type{LinearizationDual{N, T}}) where {N,T}
    Î± = zero(T)
    Î² = svector(i->zero(T),N)
    Ïµ = zero(T)
    rec = HyperRectangle(Î²,Î²)
    LinearizationDual(Î±,Î²,Ïµ,rec)
end

# domain of operation between two linearizations
function _result_domain(u::LinearizationDual{N,T},v::LinearizationDual{N,T}) where {N,T}
    # HACK: the zero rec is used when building one(::Type{LinearizationDual})
    # and zero(::Type{LinearizationDual}), so we must check those before
    # asserting equality
    zero_rec = HyperRectangle(svector(i->zero(T),N),svector(i->zero(T),N))
    if domain(u) == zero_rec
        return domain(v)
    elseif domain(v) == zero_rec
        return domain(u)
    else
        @assert domain(u) == domain(v) "domains must be identical"
        return domain(u)
    end
end
